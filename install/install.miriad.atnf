#! /bin/csh -f
#
#  This script install the linux version of ATNF miriad.
#  You should run this from a clean directory in which
#  two source tar balls are present, within which the
#  miriad tree will be built. The script 'miriad_start.csh'
#  should afterwards be sourced for others to use miriad.
#  
#  Examples to get the tar files: (remember version #'s may change)
#
#  wget -O rpfits.tar.gz ftp://ftp.atnf.csiro.au/pub/software/rpfits/rpfits-2.8.tar.gz
#  wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad.tar.gz
#

# miriad tar ball from ATNF (WARNING: miriad doesn't create the miriad tree!!!)
mkdir miriad
cd    miriad
tar zxf ../miriad.tar.gz

# remember where we are
set mir=`pwd`

# pgplot from MIRIAD/NEMO
cvs -Q co pgplot
cvs -Q co -d nemo_scripts nemo/src/scripts
nemo_scripts/pgplot.install dir=$mir lib=$mir/lib/linux host=linux \
    drivers=CGDRIV,NUDRIV,PGDRIV,PSDRIV,WDDRIV,XWDRIV 
     
setenv PGPLOT_DIR $mir/lib/linux


# rpfits
tar zxf ../rpfits.tar.gz
make -C rpfits ARCH=linux RANLIB=ranlib FC=f77 FFLAGS=-g CFLAGS=-g 
cp rpfits/librpfits.a $mir/lib/linux


./Install  <<EOF
$mir
n
y
y
y
y
EOF

set rc=miriad_start.csh

echo "# generate by $0"                                  > $rc
echo "setenv MIR $mir"                                  >> $rc
echo "setenv PGPLOT_DIR $PGPLOT_DIR"                    >> $rc
echo 'set path=($MIR/bin/linux $path); rehash'          >> $rc
echo 'setenv LD_LIBRARY_PATH $MIR/lib/linux'            >> $rc
echo 'setenv MIRPDOC $MIR/doc'                          >> $rc
echo 'setenv MIRCAT $MIR/cat'                           >> $rc
echo '#  it is quite likely a few more MIRs needed'     >> $rc

exit 0


# mir.bigbench    5392.064u 26.824s 1:34:09.40 95.9%      0+0k 0+0io 4pf+0w
