#! /bin/csh -f
#
#  This script install the linux version of ATNF miriad.
#  You should run this from a clean directory in which
#  two source tar balls are present, within which the
#  miriad tree will be built. The script 'miriad_start.csh'
#  should afterwards be sourced for others to use miriad.
#  sorry, no bash support....yet
#  
#  Examples to get the tar files: (remember version #'s may change)
#
#  Typical example of install:
#  cd $ASTROMAKE/opt/miriad
#  wget ftp://ftp.atnf.csiro.au/pub/software/rpfits/rpfits.tar.gz
#  wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad.tar.gz
#  ./install.miriad.atnf mirdir=atnf >& atnf.log

# miriad tar ball from ATNF (WARNING: their miriad tar doesn't create the miriad root tree!!!)
#
# Fall 2006:   ATNF is switching over to RCS, 
#
# To import into CVS (assuming $CVSROOT is the directory in which CVSROOT lives)
#    wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad-rcs.tar.gz
#    tar zxf miriad-rcs.tar.gz
#    mv miriad/.rcs $CVSROOT/miriadau
# To install a binary release:
#    wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad-linux.tar.gz
#    wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad-common.tar.gz
#    wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad-code.tar.gz
#    tar zxf miriad-linux.tar.gz
#    tar zxf miriad-common.tar.gz
#    tar zxf miriad-code.tar.gz
#    cd miriad
#    setenv MIR `pwd`
#    

set mirdir=miriad


foreach arg ($*)
  set $arg
end

mkdir $mirdir
cd    $mirdir
tar zxf ../miriad.tar.gz

# remember where we are
set mir=`pwd`

# pgplot from MIRIAD/NEMO
# GCC4 cannot use GIDRIV
cvs -Q co pgplot
cvs -Q co -d nemo_scripts nemo/src/scripts
nemo_scripts/pgplot.install dir=$mir lib=$mir/lib/linux host=linux \
    drivers=CGDRIV,NUDRIV,PGDRIV,PSDRIV,WDDRIV,XWDRIV 
     
setenv PGPLOT_DIR $mir/lib/linux


# rpfits
tar zxf ../rpfits.tar.gz
make -C rpfits ARCH=linux RANLIB=ranlib FC=f77 FFLAGS=-g CFLAGS=-g 
cp rpfits/librpfits.a $mir/lib/linux


./Install  <<EOF
$mir
n
y
y
y
y
EOF

set rc=miriad_start.csh

echo "# generated by $0"                                 > $rc
echo "setenv MIR $mir"                                  >> $rc
echo "setenv PGPLOT_DIR $PGPLOT_DIR"                    >> $rc
echo 'set path=($MIR/bin/linux $path); rehash'          >> $rc
echo 'setenv LD_LIBRARY_PATH $MIR/lib/linux'            >> $rc
echo 'setenv MIRPDOC $MIR/doc'                          >> $rc
echo 'setenv MIRCAT $MIR/cat'                           >> $rc
echo 'setenv MIRTEL atnf'                               >> $rc
echo '#  it is quite likely a few more MIRs needed'     >> $rc

exit 0


# mir.bigbench    5392.064u 26.824s 1:34:09.40 95.9%      0+0k 0+0io 4pf+0w
# crater:         2469.050u 10.460s 42:28.17 97.3%        0+0k 0+0io 1019pf+0w


#   below this is the new ATNF version that is maintained in RCS
#   it is only reproduced here to show what my nightly crontab does:

new_rcs:


#! /bin/csh -f
#
#  first time
#
#    setenv CVSROOT `pwd`/cvsroot       (or whereever)
#    mkdir $CVSROOT
#    cvs init
#    wget ftp://ftp.atnf.csiro.au/pub/software/miriad/miriad-rcs.tar.gz
#    tar zxf miriad-rcs.tar.gz
#    mv miriad/.rcs $CVSROOT/miriadau
#    cvs -Q co miriadau
#
#
# after you have this miriadau directory, get a new rcs, update, and diff
#

set check=cvsroot/miriadau
set ftp=ftp://ftp.atnf.csiro.au/pub/software/miriad
set tar=$ftp/miriad-rcs.tar.gz 

if (-e $check) then
  # big local assumption
  setenv CVSROOT `pwd`/cvsroot
  wget $tar >& wget.log
  if ($status) then
    echo Aborting after failed transfer $tar
    cat wget.log
    exit 1
  endif

  rm -rf $CVSROOT/miriadau.*
  mv $CVSROOT/miriadau $CVSROOT/miriadau.prev

  tar zxf miriad-rcs.tar.gz
  mv miriad/.rcs $CVSROOT/miriadau
  rm miriad-rcs.tar.gz
  rm -rf miriad

  cd miriadau
  cvs -nq up
  cvs up -dP

else
  echo Directory $check not found in `pwd`
endif


if (0) then

  # for binary releases, choose one, or any actually
  # set bins=(linux linux64 sun4sol darwin_ppc darwin_x86)
  set bins=(linux)
  foreach component (code common $bins)
    set file=miriad-$component.tar.gz
    rm -f $file
    wget $ftp/$file
    tar zxf $file
  end

  setenv MIR `pwd`
  cd $MIR
  sed -e "s,@MIRROOT@,$MIR," scripts/MIRRC.in > MIRRC
  sed -e "s,@MIRROOT@,$MIR," scripts/MIRRC.sh.in > MIRRC.sh
  
endif
