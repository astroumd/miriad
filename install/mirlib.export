#! /bin/csh -f
#
#  Generate files for subsystems like:  CARMA_TOOLS and $AIPS++/code/aips/mirlib
#  (though note as of this writing, summer 2004, AIPS++ still the old MIR3)
#  We assume here the new MIR4 version, this script produces unpredictable
#  things for MIR3
#
#  TODO:     Some linux dependant commands/options in here in the build/compile

if ($?MIR == 0) then
  echo MIR is not defined, you need to have a running MIRIAD to clone the mirlib library
  exit 1
endif

#                             command line options
set tar=0                     #  0=no tar file   1=make a tar file
set src=mirlib                #  local subdirectory name in which mirlib is built
set lib=/dev/null
set inc=/dev/null
set bin=/dev/null

# poor mans command line parser
foreach _a ($*)
  set $_a
end

if ($tar) then
  set version=`cat $MIR/VERSION`
  set src=mirlib-$version
endif

if (! -e $src) mkdir $src

cd $src
set inch=(maxdimc.h maxdim.h)
set subs=(bug.c hio.c key.c pack.c uvio.c xyzio.c dio.c headio.c interface.c maskio.c scrio.c xyio.c)
set subh=(miriad.h io.h hio.h sysdep.h)
set mkfl=(Makefile)

set cc=gcc
#set cflags=(-g -Wall)
set cflags=(-g -Wall -W -D_FILE_OFFSET_BITS=64 -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE -fPIC)

# clone files from the $MIR tree

foreach f ($inch)
  cp -p $MIRINC/$f .
end

foreach f ($subh $subs)
  cp -p $MIRSUBS/$f .
end

cp -p $MIR/install/Makefile .

echo `cat $MIR/VERSION`_mirlib > VERSION

echo "Created by the MIRIAD/install/mirlib.export script on `date`" > README.mirlib
echo "Please do not edit file, instead use full MIRIAD via CVS"    >> README.mirlib

# only compile if a tar file is not made

if ($tar) then
  echo Creating export tar $src.tar.gz 
  cd ..
  tar zcf $src.tar.gz $src
  rm -rf $src
else
  echo Compiling in $src
  foreach f ($subs)
    $cc $cflags -c $f
  end
  ar ruv libmir.a *.o
  ld -shared -o libmir.so --whole-archive libmir.a
  $cc $cflags -DTESTBED -o uvio uvio.c libmir.a -lm

  #  install
  set null=/dev/null
  if ($bin != $null) cp uvio     $bin
  if ($lib != $null) cp libmir.a $lib
  if ($inc != $null) cp miriad.h sysdep.h $inc
endif

