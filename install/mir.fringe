#! /bin/csh -f
#
#  mir.fringe:   run (standard) fringe test
#                and compare with reference map in non-mosaic mode
#                keeps a copy in $MIR/data for re-use
#  @todo   fix keep=1 for curl only operating systems
#
#  See also: 
#  http://cedarflat.mmarray.org/twiki/index.php/Array_Health_and_Regular_Maintenance#fringeCheck
#
set tmp=fringe$$
mkdir $tmp
cd $tmp

set ftp=ftp://ftp.astro.umd.edu/pub/carma/data/
set fringe=fringe.3C273.2008jun18.4.miriad.tar.gz

set mosaic=0
set keep=1
set clean=1

foreach a ($*)
  set $a
end

set targz=$MIR/data/$fringe


if (-e $targz) then
  echo Using local copy $targz 
  tar zxf $targz
else
  echo Grabbing remote copy $ftp/$fringe
  wget $ftp/$fringe
  if ($status) then
    echo No wget ... Trying curl $ftp/$fringe 
    curl $ftp/$fringe | tar zxf -
  else
    tar zxf $fringe
  endif
  if ($keep) cp $fringe $MIR/data
endif

set vis=$fringe:r:r

if (! -d $vis) then
  echo Missing fringe dataset $vis
  exit 1
endif

if ($mosaic == 0) then
  set mospars=(mosaic=0 imsize=256)
else  
  set mospars=(mosaic=1 imsize=257)  
endif
if ($clean == 1) then
  set mospars=($mospars method=clean)
else  
  set mospars=($mospars method=maxen)
endif
echo Running fringemap $mospars
echo $MIR/src/scripts/fringemap vis=$vis mask=0 $mospars '$*' > runme
chmod +x runme
./runme >& fringe.log

set refmap=vis2.cm${mosaic}.ref

if (-d $vis/$refmap) then
  maths exp="<$vis/$refmap>-<vis2.cm>" out=vis2.cm.diff
  echo HISTO Difference map:
  histo in=vis2.cm.diff
else
  echo Warning: no standard reference file present in $vis
endif

echo HISTO Dirty map:
histo in=vis2.mp | grep ^M
echo HISTO Cleaned map:
histo in=vis2.cm | grep ^M

echo Logfile in $tmp/fringe.log
