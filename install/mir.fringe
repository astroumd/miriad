#! /bin/csh -f
#
#  mir.fringe:   run (standard) fringe test
#                and compare with reference map in non-mosaic mode
#
#
#
set tmp=fringe$$
mkdir $tmp
cd $tmp

set ftp=ftp://ftp.astro.umd.edu/pub/carma/data/
set fringe=fringe.3C273.2008jun18.4.miriad.tar.gz

set mosaic=0

foreach a ($*)
  set $a
end



wget $ftp/$fringe
if ($status) then
  echo No wget ... Trying curl $ftp/$fringe 
  curl $ftp/$fringe | tar zxf -
else
  tar   zxf $fringe
endif

set vis=$fringe:r:r

echo Running fringemap:
if ($mosaic == 0) then
  set mospars=(mosaic=0 imsize=256)
else  
  set mospars=(mosaic=1 imsize=257)  
endif
$MIR/src/scripts/fringemap vis=$vis mask=0 $mospars >& fringe.log

set refmap=vis2.cm${mosaic}.ref

if (-d $vis/$refmap) then
  maths exp="<$vis/$refmap>-<vis2.cm>" out=vis2.cm.diff
  echo HISTO Difference map:
  histo in=vis2.cm.diff
else
  echo Warning: no standard reference file present in $vis
endif

echo HISTO Dirty map:
histo in=vis2.mp | grep ^M
echo HISTO Cleaned map:
histo in=vis2.cm | grep ^M
