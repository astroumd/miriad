#! /bin/csh -f
#
#  MIRIAD shell script to emulate the ClassicAIPS DDT/Y2K tests
#  (currently can only do DDT due to the nature of the Y2K visibilities)
#
#  21-aug-2001      Created                              Peter Teuben
#  

#					all  Y2K and DDT files should live here
#					see also ftp://ftp.cv.nrao.edu:/pub/aips/DDT/Y2K/
#					and      ftp://ftp.cv.nrao.edu:/pub/aips/DDT/15JAN94/
set y2kdir=/home/teuben/miriad/y2k
#					working directory
set rundir=tmpy2k_$$
#					which test (DDT, Y2K)
set test=DDT
# 					index into arrays (1=S 2=M 3=L)
set size=1
#					override previous parameters via cmdline
foreach a ($*)
  set $a
end

#			                DDT (S/M/L) and Y2K (M/L/H) problem size dependant parameters

set problem=(          S                  M                       L               H)
set uvdata=(           0                  0                       0               0)
set cell=(           1.3               1.15                     0.1             0.2)
set imsize=(         256                512                    1024            2048)
set niters=(        2000               5000                   15000           25000)
set nbox=(             1                  1                       1               2)
set box=(102,108,194,155    138,178,353,352         340,390,630,630 711,730,1061,1053,1017,1016,1367,133)
set flux=(          14.7                5.5                    0.83             200)
set noise=(          0.1               0.01                   0.001          0.0024)
set shift=(    -5.2,-5.2               6,-6           -0.049,+0.026             0,0)
set rotat=(           56                 -5                      30              23)

# (See also /AIPS/31DEC01/RUN/DDTLOAD.001)
#
# Small:  ROTAT=56; SHIFT=-5.2; CELLSIZE=1.3
#      IMSIZE=256; NITER=2000; FLUX=14.7; MAXPIXEL=6000
#      CLBOX=102,108,194,155; SCALR1 = 13.0; NOISE=0.1,0

# Medium: ROTAT=-5; SHIFT=6,-6; CELLSIZE=1.15
#      IMSIZE=512; NITER=5000; FLUX=5.5; MAXPIXEL=10000
#      CLBOX=138,178,353,352; SCALR1 = 8.4; NOISE=0.01,0

# Large:  ROTAT=30; SHIFT=-0.049,+0.026
#      CELLSIZE=0.1; IMSIZE=1024; NITER=15000; MAXPIXEL=20050
#      CLBOX=340,390,630,630; SCALR1 = 13.1;FLUX=0.83
#      ZEROSP=FLUX,0,0,0,15.0; NOISE=0.0001,0
#
# Huge:   ROTAT=0; SHIFT=0
#      CELLSIZE=0.2; IMSIZE=4096; NITER=40000; MAXPIXEL=30000
#      CLBOX=0; SCALR1=10; FLUX=250
#      ZEROSP=FLUX,300,300,0,10; NOISE=0.005,0; SCALR2=-40


if (1) then
  mkdir -p $rundir
  cd $rundir
endif

set p=$problem[$size]

#  actually have to use DDT version, since Y2K is a BINTABLE
#  miriad cannot handle those (yet). DDT still uses 

fits in=$y2kdir/${test}${p}UVDATA out=vis op=uvin


#  simple mapping and cleaning

invert vis=vis map=mp beam=bm cell=$cell[$size] imsize=$imsize[$size]
clean map=mp beam=bm out=cc niters=$niters[$size] region="box($box[$size])"
restor model=cc map=mp beam=bm out=cm

#  add selfcal gains to the visibilities
selfcal vis=vis model=cc flux=$flux[$size]

#  map again
invert vis=vis map=mp1 beam=bm1 cell=$cell[$size] imsize=$imsize[$size]
clean map=mp1 beam=bm1 out=cc1 niters=$niters[$size] region="box($box[$size])"
restor model=cc1 map=mp1 beam=bm1 out=cm1

#  maximum entropy
maxen map=mp beam=bm out=cc2 niters=$niters[$size] \
    region="box($box[$size])"  rms=$noise[$size] flux=$flux[$size]
restor model=cc2 map=mp beam=bm out=cm2


