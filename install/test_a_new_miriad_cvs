#! /bin/csh -f
#
#   Example of a script that assembles miriad from scratch and compiles
#   could be part of a nightly script.
#
#   dark-ages    -- written                      Peter Teuben
#   23-nov-05    added to CVS                    PJT
#   14-sep-06    added compile.generic support, can also re-compile current tree	PJT
#   23-jun-07    autotools based option to install                                      PJT
#
#----


#   new auto tools based?
set auto=0

#   this should be a local directory, it will fail now if you
#   give an absolute address
#   
set rootdir=miriad

#   use autoconf generated compile.generic file
set generic=1

#   reuse the current rootdir directory, instead of removing it
set reuse=0

#   options directly passed to install.miriad
set opts=()
#set opts=(intel=1 bench=32,512,1)
#set opts=(intel=1)

#   set a branch (-r <something>) or date (-D <something>)   [CVS only]
set branch=()
#set branch=(-r MIR3)
#set branch=(-r mir_3_1_1)
#set branch=(-D 12/25/04)

# set these if you don't want the system to try and figure it out
# for now CXX is not used, but for good measure, use it
# setenv CC  /usr/bin/gcc
# setenv CXX /usr/bin/g++
# setenv F77 /usr/bin/g77

set cvsroot=:pserver:anonymous@cvs.astro.umd.edu:/home/cvsroot

foreach arg ($*)
  set $arg
end

if ($?CVSROOT == 0) then
   echo No CVSROOT, trying out $cvsroot
   setenv CVSROOT $cvsroot
   grep -q `echo $cvsroot | awk -F: '{print $3}'` ~/.cvspass
   if ($status) then
      echo 'Oops, no $CVSROOT was present, '
      echo '  and this appears to be the first time you install miriad'
      echo '  so, we have to initialize your anonymous cvs account for'
      echo "  cvsroot=$cvsroot"
      echo '  anonymous cvsroot should use a blank password, just hit <RETURN>:'
      cvs login
   endif
endif

if ($?MIR) then
  echo Cannot test if MIR=$MIR is present already
  exit 1
endif

if ($reuse == 0) then
  echo "Using cvs with CVSROOT=$CVSROOT"
  echo -n "Removing your local $rootdir in 2 seconds..." 
  sleep 2
  rm -rf $rootdir
  echo "...done".
  if (1) then
    (cvs -Q co $branch -d $rootdir miriad; cd $rootdir/borrow; cvs -Q co wip; cvs -Q co rad)
  else
    (cvs -Q co $branch -d $rootdir miriad)
  endif
else
  if (! -e $rootdir) then
    echo No directory $rootdir
    exit 1
  else
    echo Reusing $rootdir
  endif
endif

if (-e  $rootdir/src/tools/ercmd.c) rm $rootdir/src/tools/ercmd.c

if ($auto) then
 cd $rootdir
 ./autogen.sh
 ./configure --prefix=$rootdir/build
 make
 make install
else
 cd $rootdir/install
 time ./install.miriad $opts generic=$generic
endif

#    patch the VERSION
set version=(`cat $rootdir/VERSION`)
echo "$version - [automated build `date`]" > $rootdir/VERSION



