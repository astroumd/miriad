#! /bin/csh -f
#
#  This script install a binary version of ATNF miriad.
#  You should run this from a clean directory. In addition
#  it will add the CARMA peculiar miriad_start scripts.
#  
#
#  Typical example of install:
#     cd $ASTROMAKE/opt/miriad
#     $MIR/install/install.miriad_bin.atnf mir=at
#  this will create a new directory 'at' within which the
#  ATNF style miriad will live
#
# Last updated:
#    15-feb-2012  modified for recent change from .gz to .bz2

set ftp=ftp://ftp.atnf.csiro.au/pub/software/miriad/

set root=.
set arch=linux64
set mir=miriad
set code=1

foreach arg ($*)
  set $arg
end

echo root=$root
echo "arch=$arch  (possible options:  linux  linux64  sun4sol  darwin_x86_64)"
echo mir=$mir
sleep 5

set files=(miriad-common.tar.bz2 miriad-$arch.tar.bz2)
if ($code) set files=($files miriad-code.tar.bz2)

if (-d $root/miriad) then
  echo Cannot install over an old version of miriad in $root
  echo This directory is temporary 
  exit 1
endif

#  get the files on the fly, or if they exist, use them
foreach file ($files)
  if (-e $file) then
     echo Using $file
     tar -C $root -jxf $file
  else
     echo Getting  $ftp/$file 
      #wget -O - $ftp/$file | tar -C $root -zxf -
     curl $ftp/$file | tar -C $root -jxf -
  endif
end

cd $root

if ($mir != miriad) then
  echo Renaming miriad to $mir
  mv miriad $mir
endif

echo Creating MIRRC and MIRRC.sh startup file
cd $mir
setenv MIR `pwd`
sed -e "s,@MIRROOT@,$MIR," scripts/MIRRC.in > MIRRC
sed -e "s,@MIRROOT@,$MIR," scripts/MIRRC.sh.in > MIRRC.sh
chmod 644 MIRRC*

# now write the new-style miriad_start.(c)sh files

