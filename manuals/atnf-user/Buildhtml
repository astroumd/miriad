#!/bin/csh -f

set out = `pwd`/userhtml

rm -rf $out
if ( ! -d $out ) mkdir $out

echo "**************** Generating the .ps files of the figures"
Buildfigs

echo "**************** Making sure the .aux file is up-to-date"
latex userhtml.tex
latex userhtml.tex

# Convert the users' guide into html form. Exit if it fails.

echo "***************** Doing the real work"

set date = (`date +"%d %h %Y"`)
set sign = "Last generated by miriad@atnf.csiro.au on $date"

setenv TEMP /tmp
latex2html -init_file ./dot.latex2html-init -info 0 -t "Miriad Users Guide" \
		-address "$sign"  userhtml.tex
if ( $status ) exit $status

#************************************************************************
echo "***************** Generate the task index file"

(cd $out; ../TaskRef.pl) > $out/taskref.txt

#************************************************************************
# Generate the task documentation.

Buildhtmltask $out

#************************************************************************
# Copy index.html into the html directory. Beware! Latex2html can make
# hard links between the index file and the "userhtml.html" file. So
# work in a way that will nullify this.

echo "***************** Copying the master .html and any gif files at last"
foreach file (*.gif *.html)
  rm -f $out/$file
  cp $file $out
end

#************************************************************************
# Copy the programmers guide across if it exists.

if ( -d $MIR/html/progguide) then
  echo "***************** Copying the programmers guide"
  cp -r $MIR/html/progguide $out
endif

#************************************************************************
# Copy the output to the final resting place??

echo -n "Move generated html files to $MIR/html? [y/n]: "
set ok = $<

if ( "$ok" == "y" ) then
  echo "Moving the html files ..."
  rm -rf $MIR/html
  mv $out $MIR/html
endif

#************************************************************************
# Tidy up

rm -rf *.dvi *.log
