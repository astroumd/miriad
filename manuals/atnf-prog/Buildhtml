#!/bin/csh -f

set out = `pwd`/progguide

rm -rf $out
if ( ! -d $out ) mkdir $out

# Generate the appendix on installation notes.

if ( -e $MIR/README ) cp $MIR/README .
unset noclobber
echo "\chapter{Installation Notes}"	>  install.inc
echo "{\small\begin{verbatim}"          >> install.inc
detab README                            >> install.inc
echo "\end{verbatim}}"                  >> install.inc

# Get the appendices on uvvariables and image items.

cp ../user/dot.latex2html-init .
cp ../user/htmldefs.inc defs.inc
cp ../user/{uvvars,image}.inc .

# Get the .aux file up-to-date.

latex progguide.tex
latex progguide.tex

set date = (`date +"%d %h %Y"`)
set sign = "Last generated by miriad@atnf.csiro.au on $date"

latex2html -init_file ./dot.latex2html-init -info 0 -t "Miriad Programmers Guide" \
		-address "$sign"  progguide.tex
if ( $status ) exit $status
cp ../user/hmiriad.gif progguide

#************************************************************************
# Copy the output to the final resting place??

echo -n "Move generated html files to $MIR/html/progguide ? [y/n]: "
set ok = $<

if ( "$ok" == "y" ) then
  echo "Moving the html files ..."
  rm -rf $MIR/html/progguide
  mv $out $MIR/html/progguide
endif

#************************************************************************
# Tidy up

rm -rf *.dvi *.log install.inc uvvars.inc image.inc README dot.latex2html-init
rm -rf defs.inc
