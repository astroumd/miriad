#!/bin/csh -f
# --------------------------------------------------------------
# pgm.prog subscript
# --------------------------------------------------------------
set mir    = $MIR
set host   = $MIRHOST
set lsys   = $mir/bin/localsys
set bindir = $mir/bin/$host
set srcdir = $mir/src
set docdir = $mir/doc/prog
set admdir = $mir/src/sys/bin
# --------------------------------------------------------------
if (-e $lsys/pgm.prog && ! $?local_compile) then
  echo $lsys/pgm.prog exists =======================
  setenv local_compile
  exec $lsys/pgm.prog $argv
endif
# --------------------------------------------------------------
if ($?Compile == 0) then
   if (-e $admdir/compile.${host}) then
      source $admdir/compile.${host}
   else
      echo "Cannot find $admdir/compile.${host} - exiting."
      exit 1
   endif
endif
# --------------------------------------------------------------
set prog_c   = $admdir/stub.prog_c
set prog_for = $admdir/stub.prog_for
#
set spec  = (`ls $srcdir/spec`)
set theDirs = (tools        prog/analysis prog/calib \
	       prog/convert prog/deconv   prog/disp  \
	       prog/map     prog/misc     prog/vis \
	       prog/atnf    prog/wsrt)
#
if (! -e $bindir/doc   ) $prog_c $srcdir/tools/doc
if (! -e $bindir/ratty ) $prog_c $srcdir/tools/ratty
if (! -e $bindir/intf2c) $prog_c $srcdir/tools/intf2c
# --------------------------------------------------------------
set manflag = "$docdir/manflag.$$"
touch $manflag
# --------------------------------------------------------------
if ($#argv == 0) then
   foreach i ($theDirs)
      pushd $srcdir/$i >& /dev/null
      foreach j (`ls *.c *.for`)
         echo ""
         echo "Processing miriad task ${j}"
	 switch ($j)
	    case hatmap.for:
	    case hatfft.for:
               echo "$srcdir/$i/$j is VMS-only:  ignored."
	       breaksw
	    case *.for:
	       $prog_for $srcdir/$i/$j
	       (cd $docdir; doc -p $srcdir/$i/$j)
	       breaksw
	    case *.c:
	       $prog_c   $srcdir/$i/$j
	       (cd $docdir; doc -p $srcdir/$i/$j)
	       breaksw
	    default:
	       breaksw
	 endsw
         echo "Finished processing miriad task ${j}"
      end
      popd >& /dev/null
   end

   foreach j ($spec)
      echo "Processing miriad task ${j}"
      if (-e $admdir/stub.${j}) then
         $admdir/stub.${j}
      else
	 echo "====== Cannot find $admdir/stub.${j}"
      endif
      (cd $docdir; $bindir/doc -p $srcdir/spec/${j}.c  )
      (cd $docdir; $bindir/doc -p $srcdir/spec/${j}.for)
      echo "Finished processing miriad task ${j}"
   end
   exit 0
endif
# --------------------------------------------------------------
foreach i ($argv)
   set in = ${i:t}
   set in = ${in:r}
   echo ""
   echo "Processing miriad task ${i:t}"
   foreach j ($theDirs)
      pushd $srcdir/$j >& /dev/null
      if (-e ${in}.for && ($in == hatmap || $in == hatfft)) then
	 echo "$srcdir/$j/${in}.for is VMS-only:  ignored."
      else if (-e $srcdir/$j/${in}.c  ) then
	 $prog_c   $srcdir/$j/${in}.c
	 (cd $docdir; doc -p $srcdir/$j/${in}.c)
      else if (-e $srcdir/$j/${in}.for) then
	 $prog_for $srcdir/$j/${in}.for
	 (cd $docdir; doc -p $srcdir/$j/${in}.for)
      endif
      popd >& /dev/null
   end
   if (-e $srcdir/spec/$in) then
      $admdir/stub.${in}
   endif
   echo "Finished processing miriad task ${i:t}"
end
# --------------------------------------------------------------
rm -fr $manflag
# --------------------------------------------------------------
unsetenv local_compile
exit 0
